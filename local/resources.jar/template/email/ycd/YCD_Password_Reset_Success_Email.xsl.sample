<?xml version="1.0" encoding="UTF-8"?>
 <!-- Licensed Materials - Property of IBM
IBM Sterling Call Center and Store
(C) Copyright IBM Corp. 2006, 2011 All Rights Reserved.
US Government Users Restricted Rights - Use, duplication or disclosure restricted by GSA ADP Schedule Contract with IBM Corp.
  --> 
<xsl:stylesheet version="1.0" 
    xmlns:xsl="http://www.w3.org/1999/XSL/Transform" 
    xmlns:emailformatters="com.yantra.pca.email.formatters"
    xmlns:java="java"
    exclude-result-prefixes="java emailformatters">

	<xsl:variable name="Brand">
		<xsl:call-template name="BrandName">
			<xsl:with-param name="BrandCode" select="/User/@EnterpriseCode"/>
		</xsl:call-template>
	</xsl:variable>

	<xsl:variable name="BrandPhoneNumber">
		<xsl:call-template name="BrandPhoneNumber">
			<xsl:with-param name="BrandCode" select="/User/@EnterpriseCode"/>
		</xsl:call-template>
	</xsl:variable>

	<xsl:template name="BrandName">
	    <xsl:param name="BrandCode"></xsl:param>
	    <xsl:choose>
		<!-- This is used for website name.  For example, YCDYourWay becomes www.YCDYourWay.com -->
		<xsl:when test="$BrandCode = 'YCD_YWE'">YCDYourWay</xsl:when>
		<xsl:when test="$BrandCode = 'YCD_BC'">YCDBusinessChoice</xsl:when>
		<xsl:otherwise><xsl:value-of select="$BrandCode"/></xsl:otherwise>
	    </xsl:choose>
	</xsl:template>

	<xsl:template name="BrandPhoneNumber">
	    <xsl:param name="BrandCode"></xsl:param>
	    <xsl:choose>
		<xsl:when test="$BrandCode = 'YCD_YWE'">1.800.YOURWAY (1.800.xxx.xxxx)</xsl:when>
		<xsl:when test="$BrandCode = 'YCD_BC'">1.800.2CHOOSE (1.800.xxx.xxxx)</xsl:when>
		<xsl:otherwise>1.800.<xsl:value-of select="$BrandCode"/> (1.800.xxx.xxxx)</xsl:otherwise>
	    </xsl:choose>
	</xsl:template>

<xsl:template name="applyStyle">
	<xsl:comment>CONTENT_TYPE=text/html; charset=UTF-8</xsl:comment>
	<HEAD>
	    <STYLE TYPE="text/css">

		.table  {
		    padding:0;
		    font-size: 12;
		    font-family: Tahoma;
		    font-weight: normal;
		    color: #000000;
		    width: 100%;
		    border: 1;
		}

		.tablecolumn{
		    padding-left:2px; 
		    padding-right:2px; 
		    padding-top: 0px;
		    padding-bottom: 0px;
		    font-size: 12;
		    vertical-align: top;
		    text-align: left;
		}
		.numerictablecolumn{
		    padding-left:2px; 
		    padding-right:2px; 
		    padding-top: 0px;
		    padding-bottom: 0px;
		    vertical-align: top;
		    text-align: right;
		    font-size: 12;
		}
		.tablecolumnheader {
		    border-left:1px solid buttonhighlight;
		    border-right:1px solid buttonshadow;
		    border-top:1px solid buttonhighlight;
		    border-bottom:1px solid buttonshadow;
		    PADDING-LEFT: 2px;
		    PADDING-RIGHT: 2px;
		    PADDING-top: 0px;
		    PADDING-bottom: 0px;
		    VERTICAL-ALIGN: middle;
		    HORIZONTAL-ALIGN: center;
		    BACKGROUND-COLOR: #e0e0e0;
		    TEXT-ALIGN: left
		}
	    </STYLE>

	</HEAD>
</xsl:template>

<xsl:template name="StandardClosingMessage">
	<xsl:param name="Brand"></xsl:param>
	<xsl:param name="BrandPhoneNumber">1.800.<xsl:value-of select="$Brand"/> (1.800.xxx.xxxx)</xsl:param>
	<xsl:param name="AdditionalClosing" />
	If we may be of further assistance, please contact us via e-mail at 

	<xsl:element name="a">
		<xsl:attribute name="href">mailto:custserv@<xsl:value-of select="$Brand"/>.com</xsl:attribute>
		custserv@<xsl:value-of select="$Brand"/>.com
	</xsl:element>

	or by calling <xsl:value-of select="$BrandPhoneNumber"/>. Our Customer Service Consultants are available 24 hours a day for your convenience.
	<xsl:copy-of select="$AdditionalClosing" />
	<p/>
	Sincerely,
	<p/>
	<xsl:value-of select="$Brand"/>.com Customer Service
</xsl:template>

	<xsl:template match="/">
		<HTML>
			<xsl:call-template name="applyStyle"/>
			<BODY topmargin="0" leftmargin="0" STYLE="font:normal 10pt Tahoma">
				<xsl:apply-templates select="User"/>
			</BODY>
		</HTML>
	</xsl:template>

	<xsl:template match="User">
		<p>Dear <xsl:value-of select="@Username"/>,</p>
		<xsl:if test="/User/User/@RequestId">
		<p>
		You are receiving this notification because we recently received a request for password reset for your account.
		</p>
		<p>
			To reset your password, click on the following link: <br/><br/>
			<a><xsl:attribute name="href">http://www.<xsl:value-of select="$Brand"/>.com/<xsl:value-of select="$Brand"/>/home/forgotPwd.action?requestId=<xsl:value-of select="/User/User/@RequestId" /></xsl:attribute>
			<xsl:attribute name="target">_blank</xsl:attribute>
			http://www.<xsl:value-of select="$Brand"/>.com/<xsl:value-of select="$Brand"/>/home/forgotPwd.action?requestId=<xsl:value-of select="/User/User/@RequestId" /></a><br/><br/>
			Once you click on the link, you will be prompted to verify yourself.  Then you can create a new password.
		</p>
		</xsl:if>
		<xsl:if test="/User/User/@GeneratedPassword">
		<p>
		Your account was recently created or modifed.
		</p>
		<p>Your login name is: <b><xsl:value-of select="@DisplayUserID" /></b>.</p>
		<p>Your Password is: <b><xsl:value-of select="/User/User/@GeneratedPassword" /></b>.</p>
		<p>
			Please reset your password the next time you log into the <a><xsl:attribute name="href">http://www.<xsl:value-of select="$Brand"/>.com/<xsl:value-of select="$Brand"/></xsl:attribute>
			<xsl:attribute name="target">_blank</xsl:attribute>
			http://www.<xsl:value-of select="$Brand"/>.com/<xsl:value-of select="$Brand"/></a> website.
		</p>
		</xsl:if>
		<p>
		<xsl:call-template name="StandardClosingMessage"><xsl:with-param name="Brand" select="$Brand"/><xsl:with-param name="BrandPhoneNumber" select="$BrandPhoneNumber"/></xsl:call-template>
		</p>
	</xsl:template>

</xsl:stylesheet>

